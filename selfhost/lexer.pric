# -*- mode: ruby -*-

#include lib/std.pric
#include lib/words.pric

def INPUT_MAX() return 80; end

def _match_kw_func(input_, pos)
  var [5]temp_str;
  var [5]pattern;

  aset(&pattern, 0, 102); # f
  aset(&pattern, 1, 117); # u
  aset(&pattern, 2, 110); # n
  aset(&pattern, 3,  99); # c
  aset(&pattern, 4,   0);

  substr(&temp_str, input_, pos, pos + 4);

  if (__and(
        str_eq(&pattern, &temp_str),
        is_ident_char(*(input_ + pos + 4)) == FALSE()
      ))
    return 4;
  else
    return 0;
  end
end

def _match_kw_var(input_, pos)
  var [4]temp_str;
  var [4]pattern;

  str_set_var(&pattern);

  substr(&temp_str, input_, pos, pos + 3);

  if (__and(
        str_eq(&pattern, &temp_str),
        is_ident_char(*(input_ + pos + 3)) == FALSE()
      ))
    return 3;
  else
    return 0;
  end
end

def _match_kw_set(input_, pos)
  var [4]temp_str;
  var [4]pattern;

  str_set_set(&pattern);

  substr(&temp_str, input_, pos, pos + 3);

  if (__and(
        str_eq(&pattern, &temp_str),
        is_ident_char(*(input_ + pos + 3)) == FALSE()
      ))
    return 3;
  else
    return 0;
  end
end

def match_kw(input_, pos)
  case
  when (0 < _match_kw_func(input_, pos))
    return _match_kw_func(input_, pos);
  when (0 < _match_kw_set(input_, pos))
    return _match_kw_set(input_, pos);
  when (0 < _match_kw_var(input_, pos))
    return _match_kw_var(input_, pos);
  else
    return 0;
  end
end

def is_ident_char(c)
  case
  when (__and(__le(48, c), __le(c, 57))) # 0..9
    return TRUE();
  when (__and(__le(65, c), __le(c, 90))) # A..Z
    return TRUE();
  when (__and(__le(97, c), __le(c, 122))) # a..z
    return TRUE();
  when (c == 95) # _
    return TRUE();
  else
    return FALSE();
  end
end

def find_non_ident_char(input_, pos_from)
  var pos = pos_from;

  var c = char_at(input_, pos);
  var do_break = FALSE();

  while (__not(do_break))
    if (is_ident_char(c))
      pos = pos + 1;
      c = char_at(input_, pos);
    else
      do_break = TRUE();
    end
  end

  return pos;
end

def match_ident(input_, pos_from)
  var pos = find_non_ident_char(input_, pos_from);
  return __sub(pos, pos_from);
end

def match_symbol(input_, pos_from)
  var c = char_at(input_, pos_from);

  case
  when (c == 40) # (
    return 1;
  when (c == 41) # )
    return 1;
  when (c == 59) # ;
    return 1;
  when (c == 61) # =
    return 1;
  when (c == 123) # {
    return 1;
  when (c == 125) # }
    return 1;
  else
    return 0;
  end
end

def match_comment(input_, pos_from)
  if (__and(
        char_at(input_, pos_from + 0) == 47, # /
        char_at(input_, pos_from + 1) == 47  # /
      ))
    return __sub(
      char_index(input_, C_LF(), pos_from),
      pos_from
    );
  else
    return 0;
  end
end

def print_kw(str_)
  putchar(107); # k
  putchar(119); # w
  putchar( 58); # :
  print_s(str_);
  putchar( 10); # LF
end

def print_ident(str_)
  putchar(105); # i
  putchar(100); # d
  putchar(101); # e
  putchar(110); # n
  putchar(116); # t
  putchar( 58); # :
  print_s(str_);
  putchar( 10); # LF
end

def print_symbol(str_)
  putchar(115); # s
  putchar(121); # y
  putchar(109); # m
  putchar( 58); # :
  print_s(str_);
  putchar( 10); # LF
end

def main()
  var [1]g;
  var [80]input; # INPUT_MAX
  var pos = 0;
  var do_break = FALSE();
  var size;
  var [8]temp_str;
  var c;

  # init globals
  *(&g + GO_ALLOC_CURSOR()) = 0;

  read_stdin_all(&input, INPUT_MAX());

  c = *(&input + pos);

  while (do_break == FALSE())
    case
    when (0 < match_kw(&input, pos))
      size = match_kw(&input, pos);
      substr(&temp_str, &input, pos, pos + size);
      print_kw(&temp_str);
      pos = pos + size;

    when (__or(c == C_SPC(), c == C_LF()))
      pos = pos + 1;

    when (0 < match_ident(&input, pos))
      size = match_ident(&input, pos);
      substr(&temp_str, &input, pos, pos + size);
      print_ident(&temp_str);
      pos = pos + size;

    when (0 < match_symbol(&input, pos))
      size = match_symbol(&input, pos);
      substr(&temp_str, &input, pos, pos + size);
      print_symbol(&temp_str);
      pos = pos + size;

    when (0 < match_comment(&input, pos))
      size = match_comment(&input, pos);
      pos = pos + size;

    else
      panic(163);
    end

    c = *(&input + pos);
    if (__or(
          c == 0,
          __le(INPUT_MAX(), pos)
        ))
      do_break = TRUE();
    end
  end
end
