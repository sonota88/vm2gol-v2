# -*- mode: ruby -*-

#include lib/std.pric
#include lib/words.pric

def _match_kw_def(input_, pos)
  var size = 3;
  var [4]s_temp;

  substr(&s_temp, input_, pos, pos + size);

  if (__and(
        str_eq_def(&s_temp),
        is_ident_char(char_at(input_, pos + size)) == FALSE()
      ))
    return size;
  else
    return 0;
  end
end

def _match_kw_var(input_, pos)
  var size = 3;
  var [4]s_temp;

  substr(&s_temp, input_, pos, pos + size);

  if (__and(
        str_eq_var(&s_temp),
        is_ident_char(char_at(input_, pos + size)) == FALSE()
      ))
    return size;
  else
    return 0;
  end
end

def _match_kw_vm_comment(input_, pos)
  var size = 4;
  var [5]s_temp;

  substr_with_check(
    &s_temp, input_, pos, pos + size,
    size + 1
  );

  if (__and(
        str_eq__cmt(&s_temp),
        is_ident_char(char_at(input_, pos + size)) == FALSE()
      ))
    return size;
  else
    return 0;
  end
end

def _match_kw_return(input_, pos)
  var size = 6;
  var [7]s_temp;

  substr_with_check(
    &s_temp, input_, pos, pos + size,
    size + 1
  );

  if (__and(
        str_eq_return(&s_temp),
        is_ident_char(char_at(input_, pos + size)) == FALSE()
      ))
    return size;
  else
    return 0;
  end
end

def _match_kw_while(input_, pos)
  var size = 5;
  var [6]s_temp;

  substr_with_check(
    &s_temp, input_, pos, pos + size,
    size + 1
  );

  if (__and(
        str_eq_while(&s_temp),
        is_ident_char(char_at(input_, pos + size)) == FALSE()
      ))
    return size;
  else
    return 0;
  end
end

def _match_kw_case(input_, pos)
  var size = 4;
  var [5]s_temp;

  substr_with_check(
    &s_temp, input_, pos, pos + size,
    size + 1
  );

  if (__and(
        str_eq_case(&s_temp),
        is_ident_char(char_at(input_, pos + size)) == FALSE()
      ))
    return size;
  else
    return 0;
  end
end

def _match_kw_end(input_, pos)
  var size = 3;
  var [4]s_temp;

  substr_with_check(
    &s_temp, input_, pos, pos + size,
    size + 1
  );

  if (__and(
        str_eq_end(&s_temp),
        is_ident_char(char_at(input_, pos + size)) == FALSE()
      ))
    return size;
  else
    return 0;
  end
end

def _match_kw_when(input_, pos)
  var size = 4;
  var [5]s_temp;

  substr_with_check(
    &s_temp, input_, pos, pos + size,
    size + 1
  );

  if (__and(
        str_eq_when(&s_temp),
        is_ident_char(char_at(input_, pos + size)) == FALSE()
      ))
    return size;
  else
    return 0;
  end
end

def _match_kw_else(input_, pos)
  var size = 4;
  var [5]s_temp;

  substr_with_check(
    &s_temp, input_, pos, pos + size,
    size + 1
  );

  if (__and(
        str_eq_else(&s_temp),
        is_ident_char(char_at(input_, pos + size)) == FALSE()
      ))
    return size;
  else
    return 0;
  end
end

def match_kw(input_, pos)
  case
  when (0 < _match_kw_def(input_, pos))
    return _match_kw_def(input_, pos);
  when (0 < _match_kw_var(input_, pos))
    return _match_kw_var(input_, pos);
  when (0 < _match_kw_vm_comment(input_, pos))
    return _match_kw_vm_comment(input_, pos);
  when (0 < _match_kw_return(input_, pos))
    return _match_kw_return(input_, pos);
  when (0 < _match_kw_while(input_, pos))
    return _match_kw_while(input_, pos);
  when (0 < _match_kw_case(input_, pos))
    return _match_kw_case(input_, pos);
  when (0 < _match_kw_end(input_, pos))
    return _match_kw_end(input_, pos);
  when (0 < _match_kw_when(input_, pos))
    return _match_kw_when(input_, pos);
  when (0 < _match_kw_else(input_, pos))
    return _match_kw_else(input_, pos);
  else
    return 0;
  end
end

def is_ident_char(c)
  case
  when (range_include(48, 57, c)) # 0..9
    return TRUE();
  when (range_include(65, 90, c)) # A..Z
    return TRUE();
  when (range_include(97, 122, c)) # a..z
    return TRUE();
  when (c == 95) # _
    return TRUE();
  else
    return FALSE();
  end
end

def find_non_ident_char(input_, pos_from)
  var pos = pos_from;

  var c = char_at(input_, pos);
  var do_break = FALSE();

  while (__not(do_break))
    if (is_ident_char(c))
      pos = pos + 1;
      c = char_at(input_, pos);
    else
      do_break = TRUE();
    end
  end

  return pos;
end

def match_ident(input_, pos_from)
  var pos = find_non_ident_char(input_, pos_from);
  return __sub(pos, pos_from);
end

def match_symbol(input_, pos_from)
  var c0 = char_at(input_, pos_from);
  var c1 = char_at(input_, pos_from + 1);

  case
  when (__and(c0 == 61, c1 == 61)) # ==
    return 2;
  when (__and(c0 == 33, c1 == 61)) # !=
    return 2;
  when (c0 == 38) # &
    return 1;
  when (c0 == 40) # (
    return 1;
  when (c0 == 41) # )
    return 1;
  when (c0 == 42) # *
    return 1;
  when (c0 == 43) # +
    return 1;
  when (c0 == C_COMMA())
    return 1;
  when (c0 == 59) # ;
    return 1;
  when (c0 == 60) # <
    return 1;
  when (c0 == 61) # =
    return 1;
  when (c0 == 91) # [
    return 1;
  when (c0 == 93) # ]
    return 1;
  when (c0 == 123) # {
    return 1;
  when (c0 == 125) # }
    return 1;
  else
    return 0;
  end
end

def match_comment(input_, pos_from)
  if (char_at(input_, pos_from + 0) == 35) # #
    return __sub(
      char_index(input_, C_LF(), pos_from),
      pos_from
    );
  else
    return 0;
  end
end

def match_int(input_, pos_from)
  var c = char_at(input_, pos_from);
  var pos;

  if (c == 45) # -
    pos = non_digit_index(input_, pos_from + 1);
  else
    pos = non_digit_index(input_, pos_from);
  end

  return __sub(pos, pos_from);
end

def match_str(input_, pos_from)
  var pos;

  if (char_at(input_, pos_from) == C_DQ())
    pos = char_index(input_, C_DQ(), pos_from + 1);
    return __sub(pos, pos_from + 1);
  else
    return 0;
  end
end

def print_kw(str_)
  putchar(107); # k
  putchar(119); # w
  putchar( 58); # :
  print_s(str_);
  putchar(C_LF());
end

def print_ident(str_)
  putchar(105); # i
  putchar(100); # d
  putchar(101); # e
  putchar(110); # n
  putchar(116); # t
  putchar( 58); # :
  print_s(str_);
  putchar(C_LF());
end

def print_symbol(str_)
  putchar(115); # s
  putchar(121); # y
  putchar(109); # m
  putchar( 58); # :
  print_s(str_);
  putchar(C_LF());
end

def print_int(str_)
  putchar(105); # i
  putchar(110); # n
  putchar(116); # t
  putchar( 58); # :
  print_s(str_);
  putchar(C_LF());
end

def print_str(str_)
  putchar(115); # s
  putchar(116); # t
  putchar(114); # r
  putchar( 58); # :
  print_s(str_);
  putchar(C_LF());
end

def INPUT_MAX() return 5000; end

def main()
  var [1]g;
  var [5000]input; # INPUT_MAX
  var pos = 0;
  var do_break = FALSE();
  var size;
  var s_temp_size = 40; var [40]s_temp;
  var c;

  # init globals
  init_alloc_cursor(&g);

  read_stdin_all(&input, INPUT_MAX());

  c = char_at(&input, pos);

  while (__not(do_break))
    case
    when (__or(c == C_SPC(), c == C_LF()))
      pos = pos + 1;

    when (0 < match_comment(&input, pos))
      size = match_comment(&input, pos);
      pos = pos + size;

    when (0 < match_symbol(&input, pos))
      size = match_symbol(&input, pos);
      substr(&s_temp, &input, pos, pos + size);
      print_symbol(&s_temp);
      pos = pos + size;

    when (0 < match_str(&input, pos))
      size = match_str(&input, pos);

      substr_with_check(
        &s_temp, &input, pos + 1, pos + 1 + size,
        s_temp_size
      );

      print_str(&s_temp);
      pos = pos + size + 2;

    when (0 < match_kw(&input, pos))
      size = match_kw(&input, pos);
      substr(&s_temp, &input, pos, pos + size);
      print_kw(&s_temp);
      pos = pos + size;

    when (0 < match_int(&input, pos))
      size = match_int(&input, pos);
      substr(&s_temp, &input, pos, pos + size);
      print_int(&s_temp);
      pos = pos + size;

    when (0 < match_ident(&input, pos))
      size = match_ident(&input, pos);
      substr_with_check(&s_temp, &input, pos, pos + size, s_temp_size);
      print_ident(&s_temp);
      pos = pos + size;

    else
      p_i(pos);
      p_c(c);
      panic(163); # not yet impl
    end

    c = char_at(&input, pos);
    if (__or(
          c == 0,
          __le(INPUT_MAX(), pos)
        ))
      do_break = TRUE();
    end
  end
end
